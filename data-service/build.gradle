buildscript {
    ext {
        springBootVersion = '2.1.3.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:1.0.2.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

plugins {
    id "com.palantir.docker" version "0.22.1"
    id "org.sonarqube" version "2.7.1"
//    id 'nebula.release' version '11.0.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
//apply plugin: 'nebula.release'

sonarqube{
    properties{
        property 'sonar.login', System.getenv('SONAR_DATA_SERVICE_TOKEN')
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'relay-service'
        property 'sonar.projectKey', 'relay-data-service'
    }
}

jar {
    archivesBaseName = 'data-service'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8
}

docker {
    name "${jar.baseName}"
    dockerfile file('Dockerfile')
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

ext {
    swaggerVersion = '2.9.0'
    apacheCommonsVersion = '3.7'

    spring = [
            web          : "org.springframework.boot:spring-boot-starter-web",
            undertow     : "org.springframework.boot:spring-boot-starter-undertow",
            security     : "org.springframework.boot:spring-boot-starter-security",
            jpa          : "org.springframework.boot:spring-boot-starter-data-jpa",
            commons      : "org.springframework.data:spring-data-commons",
            actuator     : "org.springframework.boot:spring-boot-starter-actuator",
            devtools     : "org.springframework.boot:spring-boot-devtools",
            test         : "org.springframework.boot:spring-boot-starter-test",
            security_test: "org.springframework.security:spring-security-test",


    ]

    db = [
            h2        : "com.h2database:h2",
            postgresql: "org.postgresql:postgresql"
    ]

    doc = [
            swagger   : "io.springfox:springfox-swagger2:$swaggerVersion",
            swagger_ui: "io.springfox:springfox-swagger-ui:$swaggerVersion"
    ]

    util = [
            lombok        : "org.projectlombok:lombok",
            apache_commons: "org.apache.commons:commons-lang3:$apacheCommonsVersion"
    ]
}

configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
}

dependencies {
    compile spring.web,
            spring.undertow,
            spring.security,
            spring.jpa,
            spring.commons,
            spring.actuator,
            doc.swagger,
            doc.swagger_ui,
            db.postgresql,
            util.apache_commons

    runtime spring.devtools
    testCompile spring.test,
            spring.security_test,
            db.h2
    compileOnly util.lombok
    annotationProcessor util.lombok
//    compile('org.springframework.cloud:spring-cloud-starter-config')
//    compile(':spring-cloud-starter-eureka')
    compile group: 'org.testcontainers', name: 'postgresql', version: '1.10.2'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url 'https://repo.spring.io/libs-snapshot' }
    maven { url 'https://repo.spring.io/libs-release' }
    maven { url 'https://repo.spring.io/libs-milestone' }
}


jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}
test {
    useJUnit {
        excludeCategories 'com.relay.integration.annotation.IntegrationTest'
    }
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories 'com.relay.integration.annotation.IntegrationTest'
    }
}
check.dependsOn jacocoTestReport

